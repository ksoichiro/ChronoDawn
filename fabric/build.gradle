plugins {
    id 'dev.architectury.loom'
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url 'https://maven.kyrptonaught.dev' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modApi "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modApi "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    // Custom Portal API for Fabric
    modImplementation 'net.kyrptonaught:customportalapi:0.0.1-beta66-1.21'
    include 'net.kyrptonaught:customportalapi:0.0.1-beta66-1.21'

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

loom {
    if (project(':common').loom.accessWidenerPath != null) {
        accessWidenerPath = project(':common').loom.accessWidenerPath
    }
}

// GameTest Framework configuration
fabricApi {
    configureTests {
        createSourceSet = true
        modId = "chronodawn-test"
        enableGameTests = true
        enableClientGameTests = false  // Server-side only for now
        eula = true  // By setting this to true, you agree to the Minecraft EULA
    }
}

processResources {
    from(project(':common').sourceSets.main.resources) {
        // Exclude common mixin config (use chronodawn-fabric.mixins.json instead)
        exclude 'chronodawn.mixins.json'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    // Exclude common mixin config
    exclude 'chronodawn.mixins.json'
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.minecraft_version}-fabric"
    archiveClassifier = null
}

java {
    withSourcesJar()
}

tasks.named('sourcesJar').configure {
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.minecraft_version}-fabric"
}

tasks.named('remapSourcesJar').configure {
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.minecraft_version}-fabric"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "${rootProject.archives_name}-${project.name}"
            from components.java
        }
    }

    repositories {
        // Add publishing repositories here
    }
}
